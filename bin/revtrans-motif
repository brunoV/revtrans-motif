#!/usr/bin/perl
use warnings;
use strict;
use Revtrans;

use Getopt::Long;
use Pod::Usage;

=head1 NAME

revtrans-motif - Reverse translate a Profam-like protein motif

=head1 VERSION

Version 0.01

=cut

our $VERSION = '0.01';

=head1 SYNOPSIS

From a file:

    revtrans-motif -i motifs.txt

Using pipes:

   revtrans-motif < motifs.txt > output.txt

Using interactively at the command prompt:

   $ revtrans-motif
   MAAEEL[VIKP]
   1.	ATGGCNGCNGARGARYTNVHN
   [^P]H(IW){2,3}
   2.	NDNCAY(ATHTGG){2,3}

=cut

# Parse command line arguments.
my ($help, $infile);
GetOptions( "h|help" => \$help, "i|infile:s" => \$infile );

# File IO, default to STDIN.
unless (defined $infile) { $infile = '-' };
open( my $fh, "<$infile" ) or die "Couldn't open file $infile: $!\n";

if ($help) { pod2usage(-verbose => 3) };

# Process each line, output to STDOUT.
while (my $motif = <$fh>) {
   if ($motif =~ /^\s*#/) { next }; # Ignore comments.
   chomp $motif;
   my $line = $.;

   my $result;
   eval { $result = reverse_translate_motif($motif) };
   if ($@) { warn $@; next }

   print $line, ".\t", $result, "\n";
}

=head1 DESCRIPTION

This script takes a protein motif as input and returns a degenerate
oligonucleotide sequence corresponding to it. The main reason for doing this
is to design degenerate primers that amplify a given sequence pattern.

The input motif consists of a string of one-letter residues, with any of
the following syntactic elements:

=over 4

=item B<[...]> : Redundant position.

A position in which more than one residue is allowed. Example: 

    [TS]YW[RKSD]
     ^^    ^^^^

=item B<[^...]> : Negated position.

A position in which any residue is allowed, saved for those between
brackets. Example:

    [^PW]MK[LAE]
      ^^

=item B<(...){n,m,...}> : Repeated motif.

A motif that is repeated B<n> or B<m> times. It can have any of the previous
syntactic elements. Example:

    A[SN]C(TXX){2,4,8}
           ^^^

=back

The allowed letters are those that correspond to the 20 natural aminoacids,
plus:

    B = N + D
    Z = Q + E
    X = All

=head1 OPTIONS

=head2 B<-i input-file>:

A file with a list of motifs to reverse translate.

=head2 B<-h>

Display this help message.

=cut

=head1 AUTHOR

Bruno Vecchi, C<< <vecchi.b at gmail.com> >>

=head1 BUGS

Please report any bugs or feature requests to C<< <vecchi.b at gmail.com> >>

=head1 COPYRIGHT & LICENSE

Copyright 2009 Bruno Vecchi, all rights reserved.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.


=cut
